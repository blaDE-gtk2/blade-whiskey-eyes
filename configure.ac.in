dnl whiskey-eyes-plugin - eyes that spy on you
dnl

dnl ***************************
dnl *** Version information ***
dnl ***************************
m4_define([eyes_version_major], [4])
m4_define([eyes_version_minor], [4])
m4_define([eyes_version_micro], [5])
m4_define([eyes_version_build], [r@REVISION@])
m4_define([eyes_version_tag], [])
m4_define([eyes_version], [eyes_version_major().eyes_version_minor().eyes_version_micro()ifelse(eyes_version_tag(), [git], [eyes_version_tag()-eyes_version_build()], [eyes_version_tag()])])

dnl ***************************
dnl *** Initialize autoconf ***
dnl ***************************
AC_COPYRIGHT([Copyright (c) 2006-2015 The Xfce development team])
AC_INIT([whiskey-eyes-plugin], [eyes_version], [http://bugzilla.xfce.org/], [whiskey-eyes-plugin])
AC_PREREQ([2.60])
AC_CONFIG_MACRO_DIR([m4])
AC_CANONICAL_TARGET()

dnl ***************************
dnl *** Initialize automake ***
dnl ***************************
AM_INIT_AUTOMAKE([1.9 dist-bzip2 tar-ustar no-dist-gzip])
AC_CONFIG_HEADERS([config.h])
AM_MAINTAINER_MODE()

dnl ********************************
dnl *** Check for basic programs ***
dnl ********************************
AC_PROG_CC()
AC_PROG_LD()
AC_PROG_INSTALL()
LT_PREREQ([2.2.6])
LT_INIT([disable-static])
IT_PROG_INTLTOOL([0.35.0])
AM_PROG_CC_C_O()

dnl **********************************
dnl *** Check for standard headers ***
dnl **********************************
AC_HEADER_STDC()
AC_CHECK_HEADERS([math.h])

dnl ******************************
dnl *** Check for math library ***
dnl ******************************
AC_CHECK_LIB([m], [sin])

dnl ******************************
dnl *** Check for i18n support ***
dnl ******************************
XDT_I18N([@LINGUAS@])

dnl ***********************************
dnl *** Check for required packages ***
dnl ***********************************
XDT_CHECK_PACKAGE([GTHREAD], [gthread-2.0], [2.20.0])
XDT_CHECK_PACKAGE([GTK], [gtk+-2.0], [2.14.0])
XDT_CHECK_PACKAGE([LIBBLADEUI], [libbladeui-1], [4.8.0])
XDT_CHECK_PACKAGE([LIBBLADEUTIL], [libbladeutil-1.0], [4.8.0])
XDT_CHECK_PACKAGE([LIBBLADEBAR], [libbladebar-1.0], [4.8.0])

dnl ***********************************
dnl *** Check for debugging support ***
dnl ***********************************
XDT_FEATURE_DEBUG()

AC_OUTPUT([
Makefile
po/Makefile.in
bar-plugin/Makefile
icons/Makefile
icons/48x48/Makefile
themes/Makefile
themes/Bizarre/Makefile
themes/Bloodshot/Makefile
themes/Default/Makefile
themes/Default-tiny/Makefile
themes/Horrid/Makefile
themes/Tango/Makefile
])
